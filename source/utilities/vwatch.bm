$CHECKING:OFF
SUB vwatch (linenumber AS LONG)
    STATIC AS LONG ide, bypass

    IF bypass THEN EXIT SUB

    endc$ = "<END>"

    IF ide = 0 THEN
        start! = TIMER
        DO
            ide = _OPENCLIENT("TCP/IP:9000:localhost")
            _LIMIT 30
        LOOP UNTIL ide <> 0 OR TIMER - start! > 10
        IF ide = 0 THEN bypass = -1: EXIT SUB

        me$ = COMMAND$(0) + endc$
        PUT #ide, , me$

        'start! = TIMER
        DO
            GET #ide, , temp$
            buffer$ = buffer$ + temp$
        LOOP UNTIL INSTR(buffer$, endc$) > 0 'OR TIMER - start! > 2

        buffer$ = LEFT$(buffer$, INSTR(buffer$, endc$) - 1)
        IF buffer$ <> "vwatch:ok" THEN
            CLOSE #ide
            bypass = -1
            EXIT SUB
        END IF
    END IF

    DO
        temp$ = "line number:" + MKL$(linenumber) + endc$
        PUT #ide, , temp$
        buffer$ = ""
        DO
            GET #ide, , temp$
            buffer$ = buffer$ + temp$
        LOOP UNTIL INSTR(buffer$, endc$) > 0

        buffer$ = LEFT$(buffer$, INSTR(buffer$, endc$) - 1)
        IF buffer$ = "run" THEN
            EXIT SUB
        ELSEIF buffer$ = "free" THEN
            bypass = -1
            EXIT SUB
        END IF
    LOOP
END SUB
